plugins {
    id 'java'
	id 'application'
	id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
	id 'org.openjfx.javafxplugin' version '0.0.10'
	id 'de.jjohannes.extra-java-module-info' version '0.5'
}

repositories {
    mavenCentral()
    maven {
        name "Fabric"
        url 'https://maven.fabricmc.net/'
    }
}

archivesBaseName = 'matcher'
group = 'net.fabricmc'

sourceSets {
    main {
        java.srcDirs = ['src']
		resources.srcDirs = ['res']
    }
}

javafx {
	version = "17.0.1"
	modules = [ 'javafx.controls', 'javafx.web' ]
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
}

dependencies {
    implementation 'com.github.javaparser:javaparser-core:3.24.0'
	implementation 'org.bitbucket.mstrobel:procyon-compilertools:0.5.36'
	implementation 'org.ow2.asm:asm:9.2'
	implementation 'org.ow2.asm:asm-commons:9.2'
	implementation 'org.ow2.asm:asm-tree:9.2'
	implementation 'org.ow2.asm:asm-util:9.2'
	implementation 'org.benf:cfr:0.152'
	implementation "net.fabricmc:fabric-fernflower:1.5.0"
	implementation "net.fabricmc:mapping-io:0.3.0"
}

extraJavaModuleInfo {
	automaticModule("cfr-0.152.jar", "cfr")
	automaticModule("procyon-compilertools-0.5.36.jar", "procyon.compilertools")
	automaticModule("procyon-core-0.5.36.jar", "procyon.core")
	automaticModule("fabric-fernflower-1.5.0.jar", "intellij.fernflower")
}

application {
	mainModule = 'matcher'
	mainClass = 'matcher.Main'
}

build.dependsOn(shadowJar)

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
}